def dartEnvironmentVariables = [
    APP_NAME: 'App name',
    APP_SUFFIX: null,
    APP_ENV: 'dev',
    FACEBOOK_APP_ID: null,
    FACEBOOK_CLIENT_TOKEN: null,
    TWITTER_HOST: null,
]

if (project.hasProperty('dart-defines')) {
    dartEnvironmentVariables = dartEnvironmentVariables + project.property('dart-defines')
       .split(',')
       .collectEntries { entry ->
           def pair = new String(entry.decodeBase64(), 'UTF-8').split('=')
           [(pair.first()): pair.last()]
       }
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 31

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId "com.firebase.flavor"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    buildTypes {
        release {
            // TODO: Add your own signing config for the release build.
            // Signing with the debug keys for now, so `flutter run --release` works.
            signingConfig signingConfigs.debug
        }
    }

    flavorDimensions "app"

    productFlavors {
        dev {
            dimension "app"
            resValue "string", "app_name", dartEnvironmentVariables.APP_NAME
            resValue "string", "facebook_app_id", dartEnvironmentVariables.FACEBOOK_APP_ID
            resValue "string", "facebook_client_token", dartEnvironmentVariables.FACEBOOK_CLIENT_TOKEN
            resValue "string", "twitter_host", dartEnvironmentVariables.TWITTER_HOST
            applicationIdSuffix dartEnvironmentVariables.APP_SUFFIX
        }
        stg {
            dimension "app"
            resValue "string", "app_name", dartEnvironmentVariables.APP_NAME
            resValue "string", "facebook_app_id", dartEnvironmentVariables.FACEBOOK_APP_ID
            resValue "string", "facebook_client_token", dartEnvironmentVariables.FACEBOOK_CLIENT_TOKEN
            resValue "string", "twitter_host", dartEnvironmentVariables.TWITTER_HOST
            applicationIdSuffix dartEnvironmentVariables.APP_SUFFIX
        }
        prod {
            dimension "app"
            resValue "string", "app_name", dartEnvironmentVariables.APP_NAME
            resValue "string", "facebook_app_id", dartEnvironmentVariables.FACEBOOK_APP_ID
            resValue "string", "facebook_client_token", dartEnvironmentVariables.FACEBOOK_CLIENT_TOKEN
            resValue "string", "twitter_host", dartEnvironmentVariables.TWITTER_HOST
            applicationIdSuffix dartEnvironmentVariables.APP_SUFFIX
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation platform('com.google.firebase:firebase-bom:30.0.0')
}
